group 'com.salkuadrat.biometricx'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        gradle_version = '7.0.0'
        kotlin_version = '1.5.21'
        appcompat_version = '1.3.0'
        biometric_version = '1.0.1'
        gson_version = '2.8.7'

        // ANDROID VERSION
        androidCompileSdkVersion = 30
        androidMinSdkVersion = 21
        androidTargetSdkVersion = 30

        // KOTLIN
        kotlinPlugin = "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        kotlinLib = "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        // ANDROID LIB
        androidXAppCompat = "androidx.appcompat:appcompat:$appcompat_version"

        // BIOMETRIC
        androidXBiometric = "androidx.biometric:biometric:$biometric_version"

        // GSON
        gson = "com.google.code.gson:gson:$gson_version"

        // DEPENDENCY CHECK STRATEGY
        dependencyUpdatesStrategy = {
            def isNonStable = { String version ->
                def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { keyword ->
                    version.toUpperCase().contains(keyword)
                }
                def regex = /^[0-9,.v-]+(-r)?$/
                return !stableKeyword && !(version ==~ regex)
            }
            
            componentSelection {
                all {
                    if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                        reject('Release candidate')
                    }
                }
            }
        }
    }
    
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath kotlinPlugin
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion androidCompileSdkVersion

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation kotlinLib
    implementation androidXAppCompat
    implementation androidXBiometric
    implementation gson
}
